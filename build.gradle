plugins {
    id 'fabric-loom' version '1.6-SNAPSHOT' apply false
    id 'java'
}

dependencies {
    compileOnly("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config}") {
        exclude(group: "net.fabricmc.fabric-api")
        exclude(group: "net.fabricmc.fabric-loader")
    }

    compileOnly("com.terraformersmc:modmenu:${project.modmenu}") {
        exclude(group: "net.fabricmc.fabric-api")
        exclude(group: "net.fabricmc.fabric-loader")
    }

    compileOnly "org.slf4j:slf4j-api:2.0.13"
}

allprojects {
    version = rootProject.project.mod_version + "-" + project.minecraft_version
    group = project.maven_group

    repositories {
        mavenCentral()
        maven { url "https://maven.shedaniel.me/" }
        maven { url "https://maven.terraformersmc.com/releases/" }
        maven { url "https://maven.fabricmc.net/" }

        maven {
            name = 'ParchmentMC'
            url = 'https://maven.parchmentmc.org'
        }
    }

    java {
        archivesBaseName = project.archives_base_name
    }
}

subprojects { subproject ->
    subproject.apply plugin: 'fabric-loom'
    apply from: "$rootProject.projectDir/mod.gradle"

    tasks.register('copyJars', Copy) {
        from layout.buildDirectory.dir("libs")
        into rootProject.layout.buildDirectory.dir("libs/$project.name")
    }

    tasks.build.finalizedBy(tasks.copyJars)
}